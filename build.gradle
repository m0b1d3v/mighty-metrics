import org.gradle.api.tasks.testing.logging.TestLogEvent

plugins {

	id 'application' // CLI usage
	id 'jacoco' // Test coverage

	id 'com.github.ben-manes.versions' version '0.47.0' // Dependency update checks
	id 'io.freefair.lombok' version '8.1.0' // Boilerplate reduction
	id 'org.owasp.dependencycheck' version '8.3.1' // Dependency vulnerability checks
	id 'org.sonarqube' version '4.3.0.3225' // Source code analysis

}

version = 'development'

repositories {
	mavenCentral()
}

dependencies {

	// Logging
	implementation 'ch.qos.logback:logback-classic:1.4.8'

	// Exception monitoring
	implementation 'io.sentry:sentry:6.27.0'

	// Used for JSON formatting of log messages
	// As of logback-classic:1.4.8 it includes a native JSON encoder, but it is not configurable for our purposes
	implementation 'net.logstash.logback:logstash-logback-encoder:7.4'

	// Unit tests
	testImplementation 'org.junit.jupiter:junit-jupiter:5.10.0'
	testImplementation 'org.mockito:mockito-junit-jupiter:5.4.0'

}

dependencyCheck {
	analyzers {
		assemblyEnabled = false
	}
}

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

application {
	mainClass = 'com.mobiusk.mighty.metrics.Main'
}

tasks.named('test') {

	// Enable JUnit 5
	useJUnitPlatform()

	// Put out coverage reports for tests
	finalizedBy jacocoTestReport

	testLogging {
		events = [
			TestLogEvent.PASSED,
			TestLogEvent.SKIPPED,
			TestLogEvent.FAILED,
		]
	}

}

jacocoTestReport {

	// Don't run without tests first
	dependsOn test

	// Sonar needs the XML reports
	reports {
		xml.required = true
	}

}

sonarqube {
	properties {
		property('sonar.host.url', 'https://sonarcloud.io')
		property('sonar.organization', 'mobiusk')
		property('sonar.projectKey', 'mobiusk_mighty-metrics')
	}
}
